buildscript {

    ext {
        kotlinVersion = '1.2.61'
        junitPlatformVersion = '1.0.2'
        kotlinterVersion = '1.12.0'
        kotlinLoggingVersion = '1.4.8'
        klaxonVersion = '3.0.1'

        mongodbDriverVersion = '3.6.3'

        junitVersion = '5.1.0'
        hamkrestVersion = '1.4.2.2'
        logbackVersion = '1.2.3'

        githubReleaseVersion = '1.0.9'
        grgitVersion = '2.1.1'
    }

    repositories {
        mavenCentral()
        jcenter()
        maven { url 'https://jitpack.io' }
        maven { url "https://plugins.gradle.org/m2/" }
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath "gradle.plugin.org.jmailen.gradle:kotlinter-gradle:$kotlinterVersion"
        classpath "gradle.plugin.com.github.breadmoirai:github-release:$githubReleaseVersion"
        classpath "org.ajoberstar:grgit:$grgitVersion"
    }

    configurations.classpath {
        resolutionStrategy {
            cacheDynamicVersionsFor 0, 'seconds'
            cacheChangingModulesFor 0, 'seconds'
        }
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'kotlin'
    apply plugin: 'org.jmailen.kotlinter'

    group "com.mtools.$productName"

    sourceCompatibility = 1.8

    buildscript {
        repositories {
            mavenCentral()
            jcenter()
            maven { url 'https://jitpack.io' }
            maven { url "https://plugins.gradle.org/m2/" }
        }
    }

    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()
        maven { url 'https://jitpack.io' }
    }

    dependencies {
        compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlinVersion"
        compile "io.github.microutils:kotlin-logging:$kotlinLoggingVersion"
        compile "com.beust:klaxon:$klaxonVersion"
        compile "org.mongodb:mongodb-driver:$mongodbDriverVersion"
        compile group: 'com.github.javafaker', name: 'javafaker', version: '0.15'

//        // https://mvnrepository.com/artifact/org.jetbrains.kotlin/kotlin-reflect
//        compile group: 'org.jetbrains.kotlin', name: 'kotlin-reflect', version: '1.2.61'
//        // https://mvnrepository.com/artifact/org.jetbrains.kotlin/kotlin-script-runtime
//        compile group: 'org.jetbrains.kotlin', name: 'kotlin-script-runtime', version: '1.2.61'
//        compile group: 'org.jetbrains.kotlin', name: 'kotlin-compiler-embeddable', version: '1.2.61'
//        compile group: 'org.jetbrains.kotlin', name: 'kotlin-script-util', version: '1.2.61'

        compile "org.jetbrains.kotlin:kotlin-reflect:$kotlinVersion"
        compile "org.jetbrains.kotlin:kotlin-script-runtime:$kotlinVersion"
        compile "org.jetbrains.kotlin:kotlin-compiler-embeddable:$kotlinVersion"
        compile "org.jetbrains.kotlin:kotlin-script-util:$kotlinVersion"

        testCompile "org.junit.jupiter:junit-jupiter-api:$junitVersion"
        testCompile "org.junit.jupiter:junit-jupiter-params:$junitVersion"
        testCompile "org.jetbrains.kotlin:kotlin-test"
        testCompile "org.jetbrains.kotlin:kotlin-test-junit"
        testCompile "com.natpryce:hamkrest:$hamkrestVersion"
        testCompile "io.mockk:mockk:1.8.6"

        testRuntime "org.junit.jupiter:junit-jupiter-engine:$junitVersion"
        testRuntime "ch.qos.logback:logback-classic:$logbackVersion"
    }

    archivesBaseName = "$productName-$name"

    test {
        useJUnitPlatform {
            excludeTags project.hasProperty("includeIntegrationTests") ? '' : 'integration'
        }
        testLogging {
            events "passed", "skipped", "failed"
        }
    }

    compileKotlin {
        kotlinOptions.jvmTarget = "1.8"
    }
    compileTestKotlin {
        kotlinOptions.jvmTarget = "1.8"
    }

    kotlinter {
        indentSize = 4
        continuationIndentSize = 4
        reporters = ['checkstyle', 'plain', 'html']
    }

    lintKotlinMain {
        exclude '**/vendors/*'
    }

    lintKotlinTest {
        exclude '**/vendors/*'
    }
}

apply plugin: 'com.github.breadmoirai.github-release'
apply plugin: 'org.ajoberstar.grgit'

configurations {
    archives
}

dependencies {
    archives project(path: ':cli', configuration: "archives")
}

githubRelease {
    token = project.findProperty('githubReleaseToken') ?: ''
    owner = 'christkv'
    repo = 'schema-simulator'
    name = version
    draft = true
    prerelease = true
    releaseAssets = configurations.archives.filter { it.name.endsWith("tar") || it.name.endsWith("zip") }
}
tasks.githubRelease.dependsOn 'dist'

task clean(type: Delete) {
    delete 'build/distributions'
}

task dist(type: Copy) {
    from configurations.archives.filter { it.name.endsWith("tar") || it.name.endsWith("zip") }
    into 'build/distributions'
}

task release {
    dependsOn 'githubRelease'
}
